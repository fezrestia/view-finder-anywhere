//noinspection GradleDependency
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

import java.util.regex.Pattern

android {
    compileSdkVersion 28
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId "com.fezrestia.android.viewfinderanywhere"
        minSdkVersion 26
        targetSdkVersion 28
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
    }

    task incrementVersionCode {
        def file = file("src/main/AndroidManifest.xml")
        def text = file.getText()

        // Update version code.
        def codePattern = Pattern.compile("android:versionCode=\"(\\d+)\"")
        def codeMatcher = codePattern.matcher(text)
        codeMatcher.find()
        def code = Integer.parseInt(codeMatcher.group(1))
        def newCode = code + 1
        def codeUpdatedText = codeMatcher.replaceAll("android:versionCode=\"" + newCode + "\"")

        // Update version name.
        def namePattern = Pattern.compile("android:versionName=\"(\\d+)\\.(\\d+)\\.(\\d+)-\\d+\"")
        def nameMatcher = namePattern.matcher(codeUpdatedText)
        nameMatcher.find()
        def major = Integer.parseInt(nameMatcher.group(1))
        def minor = Integer.parseInt(nameMatcher.group(2))
        def patch = Integer.parseInt(nameMatcher.group(3))
        def newName = "android:versionName=\"" + major + "." + minor + "." + patch + "-" + newCode + "\""
        def nameUpdatedText = nameMatcher.replaceAll(newName)

        file.write(nameUpdatedText)
    }

    tasks.whenTaskAdded { task ->
        if (task.name == "generateReleaseBuildConfig" || task.name == "generateDebugBuildConfig") {
            task.dependsOn("incrementVersionCode")
        }
    }

    // Define APK name and replace.
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.name == 'release') {
                if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                    outputFileName = "ViewFinderAnywhere.apk"
                }
            }
        }
    }

    productFlavors {
    }

    lintOptions {
        lintConfig file("lint.xml")
        htmlOutput file("$project.buildDir/reports/test-results-lint.html")
        xmlOutput file("$project.buildDir/reports/test-results-lint.xml")
    }
}

dependencies {
    // FireBase analytics.
    implementation 'com.google.firebase:firebase-core:17.2.0'
    // Firebase notifications.
    implementation "com.google.firebase:firebase-messaging:20.0.0"
    // Firebase crash report.
    implementation "com.google.firebase:firebase-crash:16.2.1"
    // Kotlin support.
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.50'
    implementation 'androidx.preference:preference-ktx:1.1.0'
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.50"
}

// Add for FireBase.
apply plugin: 'com.google.gms.google-services'
repositories {
    google()
    jcenter()
    mavenCentral()
}
